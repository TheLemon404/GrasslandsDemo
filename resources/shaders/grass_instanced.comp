#version 430 core

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(binding = 0, std430) buffer grassSSBO {
    mat4 grassTransformMatrices[];
};

uniform sampler2D heightMap;
uniform float heightMapStrength;
uniform vec2 terrainSpaceUVBounds;

uniform vec3 cameraPosition;
uniform mat4 cameraProjection;
uniform mat4 cameraView;
uniform float clipDistance;
uniform float frustrumClip;

uniform ivec2 terrainDimensions;
uniform ivec2 foliageChunkDimensions;
uniform ivec2 chunkCenterCutout;
uniform ivec2 numInstancesPerAxis;

#include <shader_utils.glsl>
#include <matrix_math.glsl>

void main() {
    uint x = gl_GlobalInvocationID.x;
    uint z = gl_GlobalInvocationID.y;

    vec3 scaleVector = vec3(1);

    float xp = float(x) / float(numInstancesPerAxis.x);
    float zp = float(z) / float(numInstancesPerAxis.y);

    //create initial grass blade position
    vec3 position = vec3(
            xp * foliageChunkDimensions.x,
            0,
            zp * foliageChunkDimensions.y
        );
    position -= vec3(foliageChunkDimensions.x / float(2.0), 0, foliageChunkDimensions.y / float(2.0));

    //snap to grid and then add pseudo-random offset
    float rand = random(position.xz) * 2.0f;
    position += vec3(rand, 0, rand);

    //cull if beyond terrain bounds;
    if (
        position.x < -terrainDimensions.x / 2.0 || position.x > terrainDimensions.x / 2.0 ||
            position.z < -terrainDimensions.y / 2.0 || position.z > terrainDimensions.y / 2.0
    ) return;

    //displace with heightmap
    vec2 terrainSpaceUV = ((vec2(position.x, position.z) + terrainSpaceUVBounds) / (terrainSpaceUVBounds * 2.0));
    position += vec3(0.0f, pow(texture(heightMap, terrainSpaceUV).r, 4) * heightMapStrength, 0.0f);

    //perform frustrum culling
    vec4 clipPos = cameraProjection * cameraView * vec4(position, 1.0);
    vec3 ndc = clipPos.xyz / clipPos.w;
    if (
        ndc.x < -frustrumClip || ndc.x > frustrumClip ||
            ndc.y < -frustrumClip || ndc.y > frustrumClip ||
            ndc.z < -frustrumClip || ndc.z > frustrumClip
    ) return;

    //cull if extending beyond terrain
    if (distance(position, vec3(0.0f)) > 250.0f) {
        float falloff = (distance(position, vec3(0.0f)) - 250) / 50.0f;
        scaleVector -= vec3(falloff);
        scaleVector = clamp(scaleVector, vec3(0.0f), vec3(1.0f));
    }

    mat4 transform = translate(position) * rotateY(rand) * scale(scaleVector);

    grassTransformMatrices[(x * numInstancesPerAxis.y) + z] = transform;
}
